plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.0"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

String kotlinVersion = "1.3.0"

import org.apache.tools.ant.filters.ReplaceTokens

group 'com.github.entrypointkr'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'http://nexus.hc.to/content/repositories/pub_releases' }
    maven { url 'http://repo.dmulloy2.net/content/groups/public/' }
}

dependencies {
    compile("org.spigotmc:spigot-api:1.13.1-R0.1-SNAPSHOT")
    compile("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion")
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

shadowJar {
    dependencies {
        include(dependency("org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"))
        classifier = 'stdlib'
    }
}

artifacts {
    archives shadowJar
}

processResources {
    String name = project.name
    String mainClass = [project.group, name.toLowerCase(), 'JunLibrary'].join('.')
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            'main'   : mainClass,
            'name'   : name,
            'version': project.version,
            'author' : "EntryPoint"
    ]
}
